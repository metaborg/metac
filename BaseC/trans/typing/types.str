module BaseC/trans/typing/types

imports
	include/metaC
	BaseC/trans/desugaring/constructors
	BaseC/trans/naming/names	
	lib/nbl-library.generated
	libstratego-lib
	lib/editor-common.generated
	lib/index-library.generated
	lib/analysis-library.generated
	lib/analysis-library-internal.generated
	
signature constructors
	FunType : List(Type) * Type -> Type
	MsgListType: Identifier -> Type
	
	Int	  : Type
	Flt	  : Type
	Ch    : Type
	Str   : Type
	Ptr   : Type
	
	IntOrFlt: Type
	
rules
	/*type-of: _ -> BasicType("int8")
	where
		not(is-list)
	*/
	
	/*
	 *	Constants
	 */
	
	type-of: BasicType(x) -> BasicType(x)
	
	type-of: StructType(x) -> StructType(x)
	
	//checks if val is in interval [-(posLimit+1), posLimit]; posLimit >=0
	in-range(|posLimit): strVal -> "True"
	where
		val:= <try(string-to-int)> strVal;
		<leq> (val, posLimit);
		<geq> (val, <subti> (0, <addi> (posLimit, 1)))
	 
	type-of: Decimal(val) -> type
	where
		<in-range(|127)> val;			type:= BasicType("int8")	<+
		<in-range(|32767)> val;			type:= BasicType("int16")	<+
		<in-range(|2147483647)> val; 	type:= BasicType("int32")	<+
										type:= BasicType("int64")
	
	type-of: Hexadecimal(val)-> <type-of> Decimal(<hex-string-to-int> hexval)
	where
		hexval:= <ltrim-chars(?'0'); try(ltrim-chars(?'X')); try(ltrim-chars(?'x'))> val
			
	type-of: Octal(val)	-> <type-of> Decimal(<oct-string-to-int> val)
	
	type-of: Binary(val) -> <type-of> Decimal(<bin-string-to-int> binval)
	where
		binval:= <ltrim-chars(?'0'); ltrim-chars(?'b')> val
		
	type-of: Float(a) -> BasicType("float")			//TODO: edit similar to decimal?
	
	generictype-of: BasicType("int8") -> Int()
	generictype-of: BasicType("int16") -> Int()
	generictype-of: BasicType("int32") -> Int()
	generictype-of: BasicType("int64") -> Int()
	
	generictype-of: BasicType("float") -> Flt()
	generictype-of: BasicType("double") -> Flt()
	generictype-of: BasicType("long double") -> Flt()
	
	generictype-of: Int() -> IntOrFlt()
	generictype-of: Flt() -> IntOrFlt()
	
	supertype-of: BasicType("int8") -> BasicType("int16")
	supertype-of: BasicType("int16") -> BasicType("int32")
	supertype-of: BasicType("int32") -> BasicType("int64")
	
	supertype-of: BasicType("int64") -> BasicType("float")
	supertype-of: BasicType("float") -> BasicType("double")
	supertype-of: BasicType("double") -> BasicType("long double")
	
	subtype-of: (type, type) -> None()
	
	subtype-of: (type, super) -> None()
	where
		<eq> (<supertype-of> type, super)	<+
		<subtype-of> (<supertype-of> type, super)
		 
	 
	type-of: ParamList(l) -> <map(type-of)> l
	where
		<is-list> l
	
	type-of: Param(_, ident) -> <index-type-of> ident
	
	
	//type-of: Char(_)		-> Ch()
	//type-of: String(_)		-> Str()
	//type-of: Float(_)		-> Flt()

	// Unary oprations that are always integers
   	type-of : (Size(), _) 			-> Int()
   	type-of	: (LogicalNot(), _) 	-> Int()
   	
   	// Pointers get their own type
   	type-of : (Addr(), e) 			-> Pointer(<type-of> e)
   	
   	type-of: (Deref(), e) -> derefType
   	where
   		Pointer(derefType):= <type-of> e
   	 
   	// Other unary operations
   	type-of	:	(BWCompl(), e) 	-> <type-of> e
   	type-of	:	(Neg(), e) 		-> <type-of> e
   	type-of	:	(Pos(), e) 		-> <type-of> e
   	type-of	:	(DecPre(), e)	-> <type-of> e
   	type-of	:	(IncPre(), e) 	-> <type-of> e
   	type-of	:	(DecPost(), e) 	-> <type-of> e
   	type-of	:	(IncPost(), e) 	-> <type-of> e
   	
   	// Coercing for different types (implicit conversion)
   	
   	coerce(|type): [type1, type1] -> <type-of> type1
   		where
   			type:= <generictype-of> type1
   	
   	coerce(|type): [type1, type2] -> type2
  	where
  		<subtype-of> (type1, type2);
  		type:= <generictype-of> type2
  	
  	coerce(|type): [type1, type2] -> type1
  	where
  		<subtype-of> (type2, type1);
  		type:= <generictype-of> type1
   	
  	coerce: [type1, type1] -> <type-of> type1
  	
  	coerce: [type1, type2] -> type2
  	where
  		<subtype-of> (type1, type2)
  	
  	coerce: [type1, type2] -> type1
  	where
  		<subtype-of> (type2, type1)
   	
   	coerce: [Ch(), Ch()]		-> Ch()
   	coerce: [Int(), Ch()] 		-> Int()
   	coerce: [Ch(), Int()] 		-> Int()
   	coerce: [Int(), Int()] 		-> Int()
   	coerce: [Flt(), Flt()] 		-> Flt()
   	coerce: [Int(), Flt()] 		-> Flt()
   	coerce: [Flt(), Int()] 		-> Flt()
   	coerce: [Ch(), Flt()] 		-> Flt()
   	coerce: [Flt(), Ch()] 		-> Flt()
   	//coerce: [Str(), Str()]		-> Str()	// should produce an error
   	
   	// Binary operations   	
   	type-of: (Plus(), e1, e2) 		-> <coerce> [e1,e2]
   	type-of: (Minus(), e1, e2) 		-> <coerce> [e1,e2]
   	type-of: (Mul(), e1, e2) 		-> <coerce> [e1,e2]
   	type-of: (Div(), e1, e2) 		-> <coerce> [e1,e2]
   	type-of: (Mod(), e1, e2) 		-> <coerce(|Int())> [e1,e2]
   	
   	type-of: (BWAnd(), e1, e2) 		-> <coerce> [e1,e2]
   	type-of: (BWOr(), e1, e2) 		-> <coerce> [e1,e2]
   	type-of: (BWXor(), e1, e2) 		-> <coerce> [e1,e2]	
    type-of: (BitshiftR(), e1, e2) 	-> <coerce> [e1,e2]
   	type-of: (BitshiftL(), e1, e2) 	-> <coerce> [e1,e2]
   	
   	// Types of logical operations are always integers
   	type-of: (LogicOr(), e1, e2) 	-> Int()
   	type-of: (LogicAnd(), e1, e2) 	-> Int()
   	type-of: (Eq(), e1, e2) 		-> Int()
   	type-of: (NotEq(), e1, e2) 		-> Int()
   	type-of: (GTE(), e1, e2) 		-> Int()
   	type-of: (GT(), e1, e2) 		-> Int()
   	type-of: (LTE(), e1, e2) 		-> Int()
   	type-of: (LT(), e1, e2) 		-> Int()
   	
   	// Types of expressions
   	type-of:	UnExp(op, e) 		-> <type-of> (op, <type-of> e)
   	
   	type-of: 	BinExp(op, e1, e2) 	-> <type-of> (op, <type-of> e1, <type-of> e2)
   	
   	// operators:		type-of: operator -> (operand1_type, operand2_type, result_type) 
	type-of: Plus() -> (IntOrFlt(), IntOrFlt(), IntOrFlt())
	type-of: Minus() -> (IntOrFlt(), IntOrFlt(), IntOrFlt()) 
	type-of: Mul() -> (IntOrFlt(), IntOrFlt(), IntOrFlt())
	type-of: Div() -> (IntOrFlt(), IntOrFlt(), IntOrFlt())
	
   	type-of: Mod() -> (Int(), Int(), Int())
   	
   	//bitwise operators - only ints
   	type-of: BWAnd() -> (Int(), Int(), Int())
   	type-of: BWOr() -> (Int(), Int(), Int())
   	type-of: BWXor() -> (Int(), Int(), Int())	
    type-of: BitshiftR() -> (Int(), Int(), Int())
   	type-of: BitshiftL() -> (Int(), Int(), Int())
   	
   	type-of: Neg() -> (IntOrFlt(), IntOrFlt())
   	
   	
   	//struct fields
   	//type-of: Field(_, Identifier(fieldId)) -> <index-type-of> fieldId		//TODO: modify similar to FieldViaPointer for nested structs/unions
   	type-of: Field(e, Identifier(fieldId)) -> type
   	where
   		StructType(Identifier(s)) := <type-of> e;
   		field := <index-lookup-children(|Field(), fieldId); Hd> s;
   		type := <index-type-of> field //<+
   		//type := <debug(!"5:"); index-type-of; debug(!"6:")> field //<+
   		//StructType(Identifier(s)) := <type-of> e;
   		//field := <index-lookup-children(|Struct(), fieldId); Hd> s;
   		//type := <index-type-of> field
   		//type:= <debug(!"3ns: ")> StructType(Identifier(fieldId))
   		
   		
   		
   	type-of: FieldViaPointer(e, Identifier(fieldId)) -> type
   	where
   		Pointer(StructType(Identifier(s))) := <type-of> e;
   		field := <index-lookup-children(|Field(), fieldId); Hd> s;
   		type := <index-type-of> field
   	
   	
   	debug2(s):
   		t -> t
   		where
   			<debug(s)> [t]
   		
   	index-type-of:
  		x -> <index-get-data(|Type())> Def(uri)
  	where
  		uri := <index-uri> x
   	
   	type-of: Var(Identifier(x)) -> t
   	where
   		//debug(!"1: "); 
   		//uri := <index-uri; debug(!"URI: ")> x;
   		//t := <index-get-data(|Type()); debug(!"Type: ")> Def(uri)
   		TypeSynonym(Identifier(name)) := <index-type-of> x;
   		t:= <type-of> Var(Identifier(name))						//make it recursive to get the basic type
   		<+
   		t := <index-type-of> x
   	
   	type-of: MListRef(Identifier(x)) -> <index-type-of> x
   	
   	type-of: TypeSynonym(Identifier(name)) -> <index-type-of> name
   	
   	//dummy: remove
   	//type-of: (exp, _) -> <type-of> exp
   	