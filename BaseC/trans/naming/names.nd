module BaseC/trans/naming/names

imports
  lib/analysis-library
  lib/analysis-library-internal
  lib/index-library
	include/metaC
	BaseC/trans/desugaring/constructors
	BaseC/trans/typing/types
	
namespaces
	Module
	ModuleExternal
	Import
	Function
	Variable
	Label 
	Field
	Struct
	Union
	Enum
	Enumlit
	Messagelist
	Message
	TypeSynonym
	Statemachine

//TODO: fix namebinding, refers to doesnt work for variables and fields (it does work for functions)
rules
	Block(_):
    	scopes Variable
    	
	// modules
	Module(Identifier(name), _, _):
		defines Module name
		scopes Function, Struct, Enum, Variable, Union, Messagelist, TypeSynonym, Statemachine
	
	ModuleExternal(Identifier(name), _, _):
		defines Module name
		scopes Function
		
	Import(Identifier(name)):
		refers to Module name
		imports Function from Module name
		imports Struct from Module name
		
	
	// functions
	FunDef(_,returnType, Identifier(name), params,_):
		defines Function name of type FunType(paramsTypes,returnType)
		scopes Variable, TypeSynonym
		where
			params has type paramsTypes
	FunProto(returnType,  Identifier(name), params):
		defines Function name of type FunType(paramsTypes,returnType)
		scopes Variable, TypeSynonym
		where
			params has type paramsTypes
	
	FunctionCall(Identifier(f),_): 
		refers to Function f


	// variables
	
	Param(type,Identifier(name)):
		defines Variable name of type type
	
	Declaration(type, Init(Identifier(name), _)):
		defines Variable name of type type //in subsequent scope				// currently commented because it causes unexpected problems
	//	where
	//		typ has type t 
	
	Declaration(type, NoInit(Identifier(name))):
		defines Variable name of type type //in subsequent scope				// currently commented because it causes unexpected problems
	//	where 
	//		typ has type t
	
	Declaration(type, NoInit(Identifier(varName), size)):
		defines Variable varName of type type
		
	StructOrUnionInlineDecl(Struct(_, Identifier(s), _), Identifier(name)):
		defines Variable name of type StructType(Identifier(s))
	
	StructOrUnionInlineDecl(Struct(_, None(), _), Identifier(name)):
		defines Variable name 													//TODO add type
	
	Var(Identifier(name)):
		refers to Variable name

	ArrayField(Var(Identifier(name)), _):
		refers to Variable name
		
	MListRef(Identifier(name)):
		refers to Messagelist name
	
	// structs and unions
	Union(_, Identifier(u), _):
		defines unique Union u 
		scopes Field
		
	Struct(_, Identifier(s), _):
		defines unique Struct s //of type StructType(Identifier(s))		//remove later?
		scopes Field, Struct
	
	StructType(Identifier(s)):
		refers to Struct s
		
	UnionType(Identifier(s)):
		refers to Union s
	
	FieldDecl(t, Identifier(i)):
		defines Field i of type t
	
	NestedStruct(Struct(_, Identifier(s), _), Identifier(name)):
		defines Field name of type StructType(Identifier(s))
	
	Field(e, Identifier(field)):  
		refers to Field field in Union s
		where 
			e has type Type(_, StructType(Identifier(s)))
	
	FieldViaPointer(e, Identifier(field)):
		refers to Field field in Union s
		where
			e has type Type(_, Pointer(Type(_, StructType(Identifier(s)))))
	
	TypedefDecl(type, Identifier(name)):
		defines TypeSynonym name of type type
		
	TypeSynonym(Identifier(name)):
		refers to TypeSynonym name
		refers to Struct name										//figure out another way?
		refers to Union name
		refers to Enum name
		refers to Statemachine name
		
	Enum(_, Identifier(name), _):
		defines Enum name of type BasicType("int8")					//modify to other types of int ?
		
	EnumType(Identifier(name)):
		refers to Enum name
	
	// labels and gotos	
	Label(Identifier(l), _): 
		defines Label l
	Goto(Identifier(l)): 
		refers to Label l


	// message lists and messages
	MessageList(_, Identifier(name), _):
		defines unique Messagelist name of type MsgListType(Identifier(name))
		scopes Message
		
	Message(Identifier(name), _, _, _):
 		defines Message name
 		
 	Report(var, Identifier(msg)):
 		refers to Message msg in Messagelist mList
 		where
 			var has type MsgListType(Identifier(mList))


