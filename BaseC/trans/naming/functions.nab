module BaseC/trans/naming/functions

imports
  	lib/analysis/-
  	lib/index/-
  	include/MetaC
  	BaseC/trans/analysis/desugar/constructors
  	BaseC/trans/typing/constructors
  	BaseC/trans/naming/names
  	BaseC/trans/naming/variables
  	BaseC/trans/naming/typedefs

namespaces 
	Function
	ExportedFunction
	
binding rules
	FunDef(None(), returnType, Identifier(name), ParamList(params), _):
		defines Function name of type FunType(paramsTypes, returnType')
		where params has type paramsTypes
		and returnType has type returnType'
		scopes Variable, TypeSynonym
	
	FunDef(Exported(), returnType, Identifier(name), ParamList(params), _):
		defines ExportedFunction name of type FunType(paramsTypes, returnType')
		where params has type paramsTypes
		and returnType has type returnType'
		scopes Variable, TypeSynonym
	
	FunProto(returnType,  Identifier(name), ParamList(params)):
		scopes Variable, TypeSynonym
		defines Function name of type FunType(paramsTypes, returnType)
		where params has type paramsTypes
	
	FunctionCall(Identifier(f), params): 
		refers to Function f
		otherwise refers to ExportedFunction f
		otherwise refers to Variable f 
			where f has type FunctionPointer(paramTypes, returnType)
		
	FunctionRef(Identifier(name)):
		refers to Function name
		otherwise refers to ExportedFunction name