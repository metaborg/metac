module BaseC-Types

imports 
	BaseC-Constants
	BaseC-Identifiers
	
templates
	
	PureType.Void = <void>							
	PureType.Bool = <boolean>
	PureType.Int8 = <int8>							//default: char
	PureType.Int16 = <int16>						//default: short
	PureType.Int32 = <int32>						//default: int
	PureType.Int64 = <int64>						//default long long
	PureType.Uint8 = <uint8>						//default: unsigned char
	PureType.Uint16 = <uint16>						//default: unsigned short
	PureType.Uint32 = <uint32>						//default: unsigned int
	PureType.Uint64 = <uint64>						//default: unsigned long long
	PureType.Float = <float> 						//default: 32 bit
	PureType.Double = <double> 						//default: 64 bit
	PureType.LongDouble = <long double> 			//default: 128 bit
	PureType.String = <string>
	
	
	PureType.Array = <<Type> [ <Constant?> ]>
	PureType.StructType = <struct <Identifier>>		// structs
	PureType.UnionType = <union <Identifier>>		// unions
	PureType.EnumType = <enum <Identifier>>			// enum
	PureType.TypeSynonym = <<Identifier>>			// typeSynonyms - defined by typedef
	
	Type.Type = <<TypeModifier*> <PureType>>
	Type.PointerType = <<Type> * <TypeModifier*> >

	Type.FunPointerType = < <TypeModifier*> (<Type*; separator=",">) =\> (<Type?>)>

	TypeModifier.Static = <static>
	TypeModifier.Const = <const>
	TypeModifier.Volatile = <volatile>